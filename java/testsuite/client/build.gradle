def dockerRootLocation = project.buildDir.absolutePath + "/docker"
def dockerAppLocation = dockerRootLocation + "/app"
def dockerAuthLocation = dockerRootLocation + "/auth"

dependencies {
    testCompile 'org.springframework:spring-web'
    //testCompile "com.palantir.docker.compose:docker-compose-rule-junit4"
    testCompile "junit:junit"
    testCompile "org.slf4j:slf4j-simple"
    testCompile project(':shared')
    testCompile project(':swagger/client/app')
    testCompile project(':swagger/client/auth')
}

test {
    filter {
        includeTestsMatching '*.*Suite'
    }
    doFirst {
        // Set environment variables if executed on a windows system
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            if (System.getenv('DOCKER_COMPOSE_LOCATION') == null) {
                environment('DOCKER_COMPOSE_LOCATION', 'C:\\Program Files\\Docker\\Docker\\resources\\bin\\docker-compose.exe')
            }
            if (System.getenv('DOCKER_LOCATION') == null) {
                environment('DOCKER_LOCATION', 'C:\\Program Files\\Docker\\Docker\\resources\\bin\\docker.exe')
            }
        }
    }
}

task cleanState() {
    gradle.taskGraph.whenReady { taskGraph ->
        def tasks = taskGraph.getAllTasks()
        tasks.each {
            it.setOnlyIf { true }
            it.outputs.upToDateWhen { false }
        }
    }
    setOnlyIf { true }
    outputs.upToDateWhen { false }
}

task prepareDockerInfrastructure(dependsOn: [':app:buildFatJar', ':auth:buildFatJar', 'compileJava', 'compileTestJava', 'processResources', 'processTestResources']) {
    doLast {
        def appConfig = System.getProperty("app.config") ?: ""
        def authConfig = System.getProperty("auth.config") ?: ""
        def firebaseConfig = System.getProperty("firebase.config") ?: ""
        def Pi = (System.getProperty("pi") != null) ? "-pi" : ""

        copy {
            from sourceSets.main.resources.asFileTree.matching {
                include '**/docker/app/*'
                include '**/docker/auth/*'
                include '**/docker/nginx/*'
                include '**/docker/volumes/**/*'
                include '**/docker/docker-compose' + Pi + ".yml"
                exclude '**/docker/**/dummy*'
            }
            into project.buildDir.absolutePath
            rename("docker-compose-pi.yml", "docker-compose.yml")
        }

        if (!appConfig.isEmpty()) {
            copy {
                from new File(appConfig)
                into dockerAppLocation
            }
        }
        if (!authConfig.isEmpty()) {
            copy {
                from new File(authConfig)
                into dockerAuthLocation
            }
        }
        if (!firebaseConfig.isEmpty()) {
            def firebaseConfigFile = new File(firebaseConfig)
            copy {
                from firebaseConfigFile
                into dockerAppLocation
            }
            copy {
                from firebaseConfigFile
                into dockerAuthLocation
            }
        }

        copy {
            from project(":app").jar.archivePath
            into dockerAppLocation
            rename(project(":app").jar.archiveName, "app.jar")
        }
        copy {
            from project(":auth").jar.archivePath
            into dockerAuthLocation
            rename(project(":auth").jar.archiveName, "app.jar")
        }
    }
}